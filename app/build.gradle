apply plugin: "com.android.application"
apply plugin: "me.tatarka.retrolambda"

apply from: "$project.rootDir/tools/script-checkstyle.gradle"
apply from: "$project.rootDir/tools/script-dependencies.gradle"
apply from: "$project.rootDir/tools/script-errorprone.gradle"
apply from: "$project.rootDir/tools/script-findbugs.gradle"
apply from: "$project.rootDir/tools/script-jacoco.gradle"
apply from: "$project.rootDir/tools/script-lint.gradle"
apply from: "$project.rootDir/tools/script-pmd.gradle"

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.giaquino.template"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        archivesBaseName = "app-${versionName}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
            storeFile file('../keystore/debug.jks')
        }
        release {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
            storeFile file('../keystore/debug.jks')
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt")
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        // Sets the maximum number of DEX processes that can be started concurrently.
        maxProcessCount 8
        // Sets the maximum memory allocation pool size for the dex operation.
        javaMaxHeapSize "2g"
        // Enables Gradle to pre-dex library dependencies.
        preDexLibraries true
    }
}

ext.supportVersion = "25.2.0"

dependencies {
    compile fileTree(dir: "libs", include: ["*.jar"])
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:support-compat:$supportVersion"
    compile "com.android.support:support-core-ui:$supportVersion"
    compile "com.android.support:support-fragment:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    //compile "com.android.support:percent:$supportVersion"
    //compile "com.android.support:customtabs:$supportVersion"
    //compile "com.android.support:cardview-v7:$supportVersion"
    //compile "com.android.support:gridlayout-v7:$supportVersion"
    //compile "com.android.support:palette-v7:$supportVersion"
    //compile "com.android.support:preference-v7:$supportVersion"
    //compile "com.android.support:multidex:1.0.1"
    //compile "com.android.support:mediarouter-v7:$supportVersion"
    //compile "com.android.support:support-media-compat:$supportVersion"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.6.2"
    testCompile "org.mockito:mockito-core:2.7.17"
    testCompile "org.robolectric:robolectric:3.3.1"

    androidTestCompile "com.squareup.spoon:spoon-client:1.7.1"
    androidTestCompile("com.android.support.test:rules:0.5", {
        exclude group: "com.android.support"
    })
    androidTestCompile("com.android.support.test:runner:0.5", {
        exclude group: "com.android.support"
    })
    androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support"
    })
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:2.2.2", {
        exclude group: "com.android.support"
    })
    androidTestCompile("com.android.support.test.uiautomator:uiautomator-v18:2.1.2", {
        exclude group: "com.android.support"
    })
}
